1 REM MONSTER COMBAT
2 REM WRITTEN BY LEE CHAPEL 6/15/80, TYPED FROM BYTE DEC 1980
3 REM DESIGN NOTES ARE AT THE END OF THE LISTING
4 COLOR 5,11:SCREEN 0 :GOSUB 2000
5 MM=12
9 DIMA(MM,MM):?TAB(33)"MONSTER COMBAT"
10 FOR I= 1 TO MM-1:FOR J = 1 TO MM-1:A(I,J)=1 :IF RND(1)> .75 THEN A(I,J)=2
13 NEXT:NEXT
15 X=INT(RND(1)*(MM-4)+2) :Y=INT(RND(1)*(MM-4)+2)
17 A(X,Y)=5 :C=INT(RND(1)*1501+500)
20 GOSUB 900
25 ?"YOUR COMBAT STRENGTH IS"C :? :GOSUB 400 :IF I>11 GOTO 25
30 IF M=0 THEN ?"YOU GET THE TREASURE FREE." :GOTO 495
33 IF M=100 AND N=1 GOTO 880
35 INPUT"DO YOU (1)FIGHT,(2)RUN, OR (3)BRIBE";K
40 ON K GOTO 300,350,635
297 "-----------------------------------------"
298 "                 FIGHT                   "
299 "-----------------------------------------"
300 INPUT"HOW MANY COMBAT POINTS DO YOU WISH TO USE";K
305 IF K>C THEN GOSUB 600 :?C"COMBAT POINTS" :GOTO 300
310 I=INT(RND(1)*1001):L=2:C=C-K:K=K-.01*Q
315 FOR H = 1000 TO 0 STEP -50 :IF L*M <=K AND H >= I GOTO 490
320 L=L-.1:NEXT
325 ?"THE MONSTER KILLED YOU. ";
297 "-----------------------------------------"
298 "                  DEAD                   "
299 "-----------------------------------------"
330 ?"YOU LOSE EVERYTHING" :? :INPUT"DO YOU WISH TO TRY AGAIN";X$
335 IF LEFT$(X$,1)= "Y" THEN RUN
340 ?:?"SO LONG - BETTER LUCK NEXT TIME!":END
350 I=INT(RND(1)*12):IF I=11 GOTO 325 
355 FOR H = 0 TO 10:IF H*10 > M AND H <= I GOTO 375
360 NEXT :GOTO 685
375 A=X:B=Y
380 X=INT(RND(1)*3-1)+A :Y=INT(RND(1)*3-1)+B
385 IF A(X,Y) > 1 GOTO 380
390 IF A(X,Y) = 0 GOTO 1000
395 A(A,B)=1:A(X,Y)=5 :IF I <> 12 GOTO 20
396 RETURN
397 "-----------------------------------------"
398 "              ENCOUNTER                  "
399 "-----------------------------------------"
400 I = INT(RND(1)*14+1) :M=I*10 :IF I<11 THEN ?"A ";
405 ON I GOTO 410,413,415,417,420,421,423,425,427,429,430,750,700,950
410 ?"MINOTAUR";:GOTO 450
413 ?"CYCLOPS"; :GOTO 450
415 ?"ZOMBIE";  :GOTO 450
417 ?"GIANT";   :GOTO 450
420 ?"HARPY";   :GOTO 450
421 ?"GRIFFIN"; :GOTO 450
423 ?"CHIMERA"; :GOTO 450
425 ?"DRAGON";  :GOTO 450
427 ?"WYVERN";  :GOTO 450
429 ?"BASILISK";:GOTO 450
430 ?"NOTHING"; :M=O
450 ?" IS GUARDING ";:I= INT(RND(1)*7+1)
455 ON I GOTO 460,461,463,465,467,470,475
460 ?"10 SILVER SPOONS" :P=10 :RETURN
461 ?"A JEWELED SWORD"  :P=30 :RETURN
463 ?"A JAR OF RUBIES"  :P=50 :RETURN
465 ?"A TREASURE CHEST" :P=200:RETURN
467 ?"50 SILVER COINS"  :P=50 :RETURN
470 ?"100 GOLD PIECES"  :P=100:RETURN
475 ?"A BOX OF JEWELS"  :P=75 :RETURN
490 ?"YOU BEAT THE MONSTER" :?
493 S=S+1
495 Q=Q+P
497 IF P=30  THEN GOSUB 800
500 IF P=200 THEN GOSUB 850:GOSUB 867
501 REM -----------------------------------------
502 REM                MOVEMENT                  
503 REM -----------------------------------------
505 ?"YOU NOW HAVE "Q" TREASURE POINTS"
510 A=X:B=Y:INPUT"WHAT DIRECTION (HIT 1 FOR THE MAP)"; X$
511 IF X$="1"THEN GOSUB 900:GOTO 510
512 IF RIGHT$(X$,1)="W" THEN X=X-1
513 IF RIGHT$(X$,1)="E" THEN X=X+1
514 IF LEFT$(X$,1) ="N" THEN Y=Y-1
515 IF LEFT$(X$,1) ="S" THEN Y=Y+1
517 IF A(X,Y)=0 GOTO 1000
520 IF A(X,Y)=2 THEN ?"YOU RAN INTO A WALL":X = A:Y = B:GOTO 510 
525 A(A,B)=1:A(X,Y)=5:GOTO 20
597 "-----------------------------------------"
598 "                BRIBERY                  "
599 "-----------------------------------------"
600 ?"YOU ONLY HAVE" ; :RETURN
635 INPUT"HOW MUCH DO YOU WISH TO PAY";K
640 IF K>Q THEN GOSUB 600:?Q"TREASURE POINTS":GOTO 635
645 I=INT(RND(1)*22) :L=O :IF I=21 OR K<1 GOTO 325
660 FOR H=0 TO 20 :IF K<=L*P AND I>=H GOTO 680
670 L=L+.1:NEXT
675 ?"YOUR BRIBE WAS ACCEPTED.";:Q=Q-K:GOTO 505
680 ?"YOUR BRIBE WAS NOT ACCEPTED. ";
685 ?"YOU MUST FIGHT.":GOTO 300
697 "-----------------------------------------"
698 "              GIANT BAT                  "
699 "-----------------------------------------"
700 ?"A GIANT BAT CARRIED YOU TO A NEW SPOT." :?:A=X :B=Y
705 X=INT(RND(1)*(MM-3)+2) :Y =INT(RND(1)*(MM-3)+2):IF A(X, Y) <> 1 GOTO 705
710 A(A,B)=1 :A(X,Y)=5:RETURN
747 "-----------------------------------------"
748 "                PITFALL                  "
749 "-----------------------------------------"
750 J=INT(RND(1)*20+1) :C=C-J
755 ?"YOU FELL INTO A PIT AND USED"J"COMBAT POINTS TO CLIMB OUT." :?
760 IF C<O THEN ?"YOU DIED WHILE CLIMBING OUT.":GOTO 330
765 GOTO 375
797 "-----------------------------------------"
798 "            ENCHANTED SWORD              "
799 "-----------------------------------------"
800 IF RND(1)<.5 OR C>3000 THEN RETURN
810 ?"THE SWORD WAS ENCHANTED AND DOUBLES YOUR STRENGTH!" :?
815 C=C*2:RETURN
847 "-----------------------------------------"
848 "                  TRAP                   "
849 "-----------------------------------------"
850 I=INT<RND(1)*10):IF I<>7 THEN RETURN
860 ?"THE CHEST WAS A TRAP. YOU WERE KILLED WHEN YOU OPENED IT." :?
865 GOTO 330
866 "---------------- MIRROR -----------------"
867 I=INT(RND(1)*10): IF I <> 3 THEN RETURN
870 ?"A MIRROR WAS IN THE CHEST. ";
875 ?"IT WILL KILL ANY BASILISKS YOU MEET." :?:N=1 :RETURN
880 ?"YOUR MIRROR KILLED THE BASILISK.": :?GOTO 493
897 "-----------------------------------------"
898 "              PRINT THE MAP              "
899 "-----------------------------------------"
900 FOR I=0 TO MM: FOR J=0 TO MM :?TAB(5); :IF A(J,I)=0 THEN ?" ";
901 IF A(J,I)=1 THEN ? CHR$(193);
902 IF A(J,I)=2 THEN ?"I";
903 IF A(J,I)=5 THEN ? CHR$(215);
905 NEXT:?:NEXT:RETURN
950 IF S<5 GOTO 400
955 ?"A GIANT EAGLE CARRIED YOU TO SAFETY." :?
1000 ?"YOU SURVIVED THE FOREST." :?
1005 ?"YOU WON A TREASURE TOTAL OF"Q: ?"CONGRATULATIONS!"
1010 END
1997 "-----------------------------------------"
1998 "              INSTRUCTIONS               "
1999 "-----------------------------------------"
2000 SCREEN 1
2001 ? "MONSTER COMBAT" :?
2010 ? "ATTEMPT TO WIN TREASURE FROM VARIOUS LOATHSOME CREATURES AND LEAVE"
2020 ? "THE FOREST WITH AS MUCH TREASURE AS POSSIBLE - WITHOUT GETTING"
2030 ? "KILLED IN THE PROCESS." :?
2040 ? "IN THE GAME, YOU ARE PLACED AT A RANDOMLY CHOSEN LOCATION IN A FOREST"
2050 ? "WITH A CERTAIN COMBAT STRENGTH THAT IS RANDOMLY DETERMINED. ALSO, YOU"
2060 ? "ENCOUNTER MONSTERS GUARDING VARIOUS TEMPTING TREASURES AND ARE GIVEN"
2070 ? "A CHOICE OF FIGHTING THEM, RUNNING FROM THEM, OR BRIBING THEM; GREED"
2080 ? "AND WIT ARE CERTAINLY TESTED HERE. " :?
2100 ? "MOVEMENT" :?
2110 ? "MOVEMENT THROUGH THE FOREST IS EASY : JUST ENTER THE DIRECTION YOU"
2120 ? "WANT TO GO - N FOR NORTH, E FOR EAST, NE FOR NORTHEAST, AND SO ON."
2130 ? "NORTH IS THE TOP OF THE MAP AND EAST IS THE RIGHT. " CHR$(193) " IS"
2140 ? "FOREST IN THE DISPLAY, THE LETTER I REPRESENTS A WALL THROUGH WHICH"
2150 ? "YOU CANNOT PASS, AND THE " CHR$(215) " IS YOU. TO LEAVE THE FOREST,"
2160 ? "JUST MOVE ONTO ANY BLANK AREA AROUND THE EDGE OF FOREST. " :?
2170 ? "SCORES" :?
2171 ? "SCORES FOR THIS GAME GENERALLY RANGE FROM 500 TO 700. IF YOU GET OVER"
2172 ? "1000, YOU ARE DOING VERY WELL." :?
2180 ? " --- PRESS A KEY TO CONTINUE --- "
2185 GET A$: IF A$="" GOTO 2185
2190 CLS
2200 ? "OPTION 1 - FIGHT" :?
2201 ? "TO FIGHT THE MONSTER, ENTER A 1. WHEN THE PROGRAM ASKS YOU FOR THE"
2210 ? "NUMBER OF COMBAT POINTS YOU WISH TO USE, ENTER ANY NUMBER SMALLER"
2220 ? "THAN YOUR COMBAT STRENGTH (IE: THE AMOUNT YOU THINK IT WILL TAKE TO"
2230 ? "DEFEAT THE MONSTER). EACH MONSTER HAS ITS OWN COMBAT STRENGTH. IF YOU"
2240 ? "ENTER A NUMBER OF COMBAT POINTS EQUAL TO THE MONSTER'S STRENGTH, YOU"
2250 ? "HAVE A FIFTY-FIFTY CHANCE OF DEFEATING IT. ENTERING A HIGHER NUMBER"
2260 ? "INCREASES YOUR CHANCES OF WINNING, WHILE ENTERING A SMALLER NUMBER"
2270 ? "LOWERS YOUR CHANCE OF WINNING. ALSO, THE MORE TREASURE YOU HAVE, THE"
2280 ? "MORE STRENGTH YOU NEED TO USE TO ACHIEVE THE SAME ODDS AGAINST THE"
2290 ? "MONSTER IN COMBAT." :?
2295 REM ? " --- PRESS A KEY TO CONTINUE --- "
2296 REM GET A$: IF A$="" GOTO 2296
2299 REM CLS
2300 ? "OPTION 2 - RUN" :?
2310 ? "IF YOU CHOOSE TO RUN (OPTION 2), YOUR CHANCES OF GETTING AWAY FROM A"
2320 ? "MONSTER SUCCESSIVELY DECREASE AS EACH NEW MONSTER'S STRENGTH POSSIBLY"
2330 ? "INCREASES. THUS YOU ARE MORE LIKELY TO GET AWAY FROM A MINOTAUR THAN"
2340 ? "FROM A BASILISK. IF YOU SUCCEED IN RUNNING AWAY, YOU ARE PLACED IN A"
2350 ? "NEW RANDOMLY SELECTED SQUARE; NOT GETTING AWAY USUALLY RESULTS IN A"
2360 ? "BATTLE. OCCASIONALLY, WHILE YOU ARE RUNNING, THE MONSTER WILL CATCH"
2370 ? "AND KILL YOU - ONE OF THE RISKS YOU MUST ACCEPT. " :?
2380 ? " --- PRESS A KEY TO CONTINUE --- "
2381 GET A$: IF A$="" GOTO 2381
2390 CLS
2400 ? "OPTION 3 - BRIBE" :?
2410 ? "OF COURSE TRYING TO BRIBE (OPTION 3) YOUR FOE IS AN AGE OLD"
2420 ? "ALTERNATIVE TO RUNNING OR FIGHTING. FOR THIS, YOU MUST USE YOUR HARD-"
2430 ? "EARNED TREASURE. WHETHER OR NOT YOU WIN DEPENDS ON THE VALUE OF THE"
2440 ? "TREASURE HE IS GUARDING. THE GREATER THE TREASURE, THE MORE YOU HAVE"
2450 ? "TO PAY TO SUCCESSFULLY BRIBE THE MONSTER. IF HE DOESN'T CARE FOR YOUR"
2460 ? "BRIBE, YOU USUALLY HAVE TO FIGHT HIM. " :?
2480 REM ? " --- PRESS A KEY TO CONTINUE --- "
2481 REM GET A$: IF A$="" GOTO 2481
2490 REM CLS
2500 ? "OTHER THINGS" :?
2510 ? "THERE ARE OTHER THINGS IN THE FOREST BESIDES MONSTERS, BUT THESE ARE"
2520 ? "BEST LEFT FOR THE PLAYER TO DISCOVER. ONE THING THAT I WILL MENTION"
2530 ? "IS THE MIRROR YOU MAY FIND. THIS KILLS BASILISKS, THE MOST FEARSOME"
2540 ? "OF ALL THE CREATURES YOU WILL MEET. A BASILISK CAN KILL PEOPLE BY"
2550 ? "LOOKING THEM IN THE FACE, BUT WHEN IT LOOKS INTO A MIRROR IT"
2560 ? "FRIGHTENS ITSELF TO DEATH." :?
2570 ? " --- PRESS A KEY TO CONTINUE --- "
2996 REM --------------------------------------
2997 REM            SEED THE PRNG
2998 REM --------------------------------------
2999 N=-1
3000 GET A$
3010 N=N-1:IF N<-30000 THEN N = -1
3020 IF A$="" GOTO 3000
3030 ZZ=RND(N): REM SEED THE PRNG
3040 SCREEN 0
9999 RETURN
10000 "==================================================================
10001 ""
10002 "                         DESIGN NOTES
10003 "
10010 "THIS PROGRAM WAS IN BASIC ON A KIM-1 MICROCOMPUTER WITH 4 K BYTES"
10011 "OF MEMORY. IT IS EASY TO MODIFY TO RUN ON A DIFFERENT MACHINE OR TO"
10012 "GENERATE A LARGER (OR SMALLER) FOREST TO EXPLORE."

