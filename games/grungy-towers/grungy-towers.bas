10 REM --------------------------------------
11 REM      GENTLE X16 MODS PLACED HERE       
20 COLOR 5,0: SCREEN 3
90 REM --------------------------------------
100 REM PROGRAM BY JIM BUTTERFIELD
110 REM    TOWERS ARCHITECTURE BY JOHN STOUT
120 I=RND(-TI)
130 DN$="\X11\X11\X11\X11\X11\X11\X11\X11\X11\X11"
140 POKE59468,12:DEFFNR(X)=INT(RND(1)*X)+1
150 PRINT"\X93\X8E\X11     GRUNGY TOWERS"
160 PRINT"\X11A MURDER HAS BEEN ";
170 Z$="COMMITTED AT THE TOWERS.  YOUR JOB,  SUPERINTENDENT, ":GOSUB 4000
180 Z$="IS TO FIND THE MURDERER.\X11 ":GOSUB 4000
190 Z$="CHECK THE ALIBIS CLOSELY: THE KILLER IS SURE TO GIVE ":GOSUB 4000
200 Z$="HIMSELF AWAY.\X11":GOSUB 3900
210 Z$="HINT: NOBODY EVER SUSPECTS THE REAL MURDERER.":GOSUB 3900
230 Z$="PLAYING HINT:  NAME ANY ROOM OR PERSON USING ":GOSUB 4000
240 Z$="THREE OR MORE LETTERS, E.G., 'LIV' FOR LIVING ROOM.\X11":GOSUB 3900
270 DIMM$(25),R$(25),K$(4),Y$(6)
271 DIMS(12),N$(12),C$(12),A$(12),B$(12),R%(12)
272 DIMW(12),X(12)
277 REM --------------------------------------
278 REM             MURDER WEAPONS
279 REM --------------------------------------
280 DATA SHOT WITH A ANTIQUE PISTOL,STABBED WITH AN ORIENTAL DAGGER
290 DATA STRANGLED BY A SILKEN SASH,CLUBBED WITH A BRASS CANDLESTICK
300 DATA MANGLED BY A FALLEN CHANDELIER,SMOTHERED WITH AN ERMINE PILLOW
310 FORI=1TO6:READY$(I):NEXTI:Y%=FNR(6)
341 REM --------------------------------------
345 REM             YOU GET 4 TRIES:
349 REM --------------------------------------
320 DATA SUPERINTENDENT,INSPECTOR,SERGEANT,CONSTABLE
330 FORI=1TO4:READK$(I):NEXTI:K%=1
340 FORI=1TO25:READR$(I):NEXT
341 REM --------------------------------------
345 REM             LOCATIONS
349 REM --------------------------------------
350 DATAHALL,LIVING ROOM,KITCHEN,DINING ROOM,BILLIARD ROOM,DOWNSTAIRS LAV
360 DATABATHROOM,MASTER BEDROOM,NURSERY
370 DATASERVANT'S QUARTERS
380 DATACONSERVATORY,TERRACE,STABLES,ATTICS,LAUNDRY,LIBRARY,MUSIC ROOM,STUDY
390 DATABOILER ROOM,PANTRY,SCULLERY
400 DATAFIRST BEDROOM,SECOND BEDROOM,THIRD BEDROOM,FOURTH BEDROOM
410 FORI=1TO25:M$(I)="":NEXT
420 I=1
430 J=FNR(24)+1:IFM$(J)=""THENGOSUB3000:I=J:GOTO430
460 FORI=1TO25:IFM$(I)<>""THENNEXTI:GOTO500
470 J=FNR(25):IFM$(J)=""GOTO470
480 GOSUB3000:GOTO460
500 FORI=1TO12:READN$(I),C$(I),A$(I),B$(I)
510 NEXTI
520 FORI=1TO25:IFLEN(M$(I))>1GOTO560
530 FORJ=I+1TO25:IFLEN(M$(J))>1GOTO550
540 GOSUB3000
550 NEXTJ
560 NEXTI
562 PRINT"PRESS ANY KEY TO START."
563 GETZ$:IFZ$=""GOTO563
566 PRINT"\X93":GOSUB2000
567 REM --------------------------------------
568 REM           DRAMATIS PERSONAE
569 REM --------------------------------------
570 DATA FUNGUS,BUTLER,"SIR, I'M AFRAID I KNOW NOTHING.",MOST REGRETTABLE.
580 DATA MRS RAMSBOTTOM,COOK,"DON'T KNOW A THING, DEARIE.","ISN'T IT AWFUL?"
590 DATA BLOGGS,GARDENER,"WELL,GUV,IT  BEATS ME.","IT'S A RUM GO"
600 DATA FIFI,MAID,"EET EES SO SURPRISEENG!","HOW YOU SAY, SCARY.."
610 DATA CHOLMONDELY,COLONEL,"BY JOVE!  AWKWARD BUSINESS, THIS.","BOTHER!"
620 DATA LADY THROCKMORTON,DOWAGER,"DEAR ME..","IT'S VERY BAD MANNERED"
630 DATARAGS,CHAUFFEUR,"DON'T KNOW NOTHING, SIR:","MAKES YOU THINK, DON'T IT?"
640 DATA AGATHA FFORBES-SMYTHE,DEBUTANTE,GOODNESS!,"ISN'T IT JUST AWFUL?"
650 DATA TINSEL,ACTRESS,"DON'T KNOW A THING, DARLING.","HOW DRAMATIC, THOUGH!"
660 DATA MADAME BLINSKY,COUNTESS,"I SEE NODINK, MY DEAR.",IS VERY SINISTER
670 DATA FRANK N STONE,DOCTOR,"BAD BUSINESS, THIS.","DON'T LIKE IT AT ALL"
680 DATA PIGGY FANSHAW,TWIT,"I SAY, EXCITING, WHAT?","JOLLY BAD SHOW, OLD MAN"
700 R%=FNR(24)+1
710 FORI=1TO12:W%=FNR(25)
720 X%=FNR(25):IFX%=R%GOTO720
730 W(I)=W%:X(I)=X%:NEXTI
740 GOSUB2000:M%=FNR(12):X(M%)=0:X=R%
750 G%=FNR(3):ONG%GOTO780,810
760 FORI=1TO11:X=X(I):FORJ=I+1TO12:IFX=X(J)GOTO810
770 NEXTJ,I:G%=1
780 Z%=1:X=FNR(25):FORI=1TO12:IFX(I)=XGOTO810
790 NEXTI
800 Z%=FNR(12):IFM%=Z%GOTO800
810 X(M%)=-X:GOSUB2000:FORI=1TO12
820 S(I)=FNR(12):NEXTI
830 GOSUB2080:R=1:PRINT"\X93"
840 PRINT "YOU ARE ";:X=R:GOSUB 3500:Z$=R$:GOSUB 3900
850 PRINT:FORI=1TO12:IFW(I)<>RGOTO1020
860 Z$=N$(I)+",THE "+C$(I)+", IS HERE.  ":GOSUB 4000
870 Z$="THE "+C$(I)+" SAYS:":GOSUB 4000
875 Z$="'"+A$(I)+"  ":IF R%(I)>0 THEN Z$="'I ALREADY TOLD YOU:  "
880 R%(I)=1:GOSUB4000:Z$="I WAS ":
882 X=ABS(X(I)):GOSUB 3400:Z$=Z$+R$
890 Z$=Z$+"AT THE TIME ":GOSUB4000
900 IFI=M%ANDZ%=0GOTO960
910 Z$="":W$="WITH THE ":FORJ=1TO12:IFI=JGOTO930
920 IF X<>X(J) GOTO 930
921 IF Z$<>"" THEN Z$=Z$+" ":GOSUB 4000:W$="AND THE "
922 Z$=W$+C$(J)
930 NEXTJ
940 IF Z$<>"" THEN Z$=Z$+".  ":GOSUB 4000:GOTO 970
950 IFI=M%ANDZ%>0THENZ$=W$+C$(Z%)+".  ":GOSUB4000:GOTO970
960 Z$="BY MYSELF.  ":GOSUB4000
970 S%=S(I):IFS%=M%THENZ$="MAYBE IT WAS SUICIDE. ":GOSUB4000:GOTO1010
990 IFABS(X(S%))=XTHENZ$="I DUNNO WHO COULD HAVE DONE IT.  ":GOSUB4000:GOTO1010
1000 Z$="I SUSPECT THE ":GOSUB4000:Z$=C$(S%)+".  ":GOSUB4000
1010 Z$=B$(I):GOSUB4000:PRINT".'":PRINT:FORJ=1TO2E3:NEXT
1020 NEXTI
1030 IFR%<>RGOTO1070
1040 Z$="THERE IS A DEAD BODY HERE "+Y$(Y%)+".":GOSUB 3900
1060 Z$="THE BODY SAYS: NOTHING":GOSUB 3900:PRINT
1070 M$=M$(R)
1080 PRINT"YOU MAY \X12 ACCUSE \X92, ";
1085 Z$="OR PROCEED TO:":GOSUB 3900
1090 FORI=1TOLEN(M$):J=ASC(MID$(M$,I))-64:?" - " R$(J):NEXTI
1100 PRINT
1110 INPUT"YOUR MOVE  \XBF\X9D\X9D\X9D";X$
1120 IF LEN(X$)<3GOTO1080
1130 X$=LEFT$(X$,3):IFX$="ACC"GOTO1170
1140 FORI=1TOLEN(M$):J=ASC(MID$(M$,I))-64
1150 IFLEFT$(R$(J),3)=X$THENR=J:GOTO840
1160 NEXTI:GOTO1080
1170 PRINT"VERY WELL, ";K$(K%):PRINT" YOU ACCUSE THE...";
1180 INPUTA$:IFLEN(A$)<3GOTO1080
1190 A$=LEFT$(A$,3):FORI=1TO12:IFA$<>LEFT$(C$(I),3)THENNEXTI:GOTO1080
1200 IFI=M%GOTO1250
1210 PRINT"BUNGLER!  IT'S NOT THE ";C$(I);"!"
1220 K%=K%+1:IFK%>4GOTO1270
1230 PRINT"YOU ARE DEMOTED TO ";K$(K%);"!"
1240 PRINT"GO BACK AND TRY TO GET THE RIGHT ONE!":GOTO1080
1250 PRINT"\X12 RIGHT! \X92  ANOTHER CRIMINAL SPEEDILY"
1260 PRINT"BROUGHT TO JUSTICE!"
1270 PRINT:PRINT "  SOLUTION:"
1280 PRINT "THE ";C$(M%);" ";
1290 IF R%(M%)=0 THEN Z$="WOULD HAVE ":GOSUB 4000
1300 IF G%=2 THEN Z$="ADMITTED BEING IN THE MURDER ROOM!":GOTO 1390
1310 Z$="CLAIMED TO BE ":GOSUB 4000
1320 X=ABS(X(M%)):GOSUB 3500:Z$=R$:GOSUB4000
1330 W$="WITH THE ":IF Z%=0 THEN W$="ALONE.  BUT THE "
1340 F=0:FOR J=1 TO 12:IF M%=J GOTO 1360
1350 IFX=X(J)THENZ$=W$:GOSUB4000:Z$=C$(J)+" ":GOSUB4000:W$="AND THE ":F=1
1360 NEXTJ:IF F=0 AND Z%>0 THEN F=1:Z$=W$:GOSUB4000:Z$=C$(Z%):GOSUB4000
1370 IF F=1 THEN Z$=" ... WHO WON'T BACK UP THIS ALIBI."
1380 IF Z%=0 THEN Z$="WERE THERE..."
1390 GOSUB 4000
1400 PRINT:END
1997 REM --------------------------------------
1998 REM       PRINT THE GRUNGY TOWERS
1999 REM --------------------------------------
2000 N=INT((RND(1)*30)+2)
2010 L=FNR(6)*2
2020 PRINT"\X13";LEFT$(DN$,L)
2030 GOSUB3010
2040 FORI=L+9TO20
2050 IFI>15ANDI<19THENPRINTTAB(N+1)"\XA6\XDB\XDB\XA6\XA5":GOTO2070
2060 PRINTTAB(N+1)"\XA6\XA6\XA6\XA6\XA5"
2070 NEXT:RETURN
2080 FORJ=0TO15+RND(1)*8:GOSUB2000:NEXTJ
2090 PRINT"\X13"DN$;LEFT$(DN$,6)
2100 PRINTTAB(N+1)"\XA9 \XDF"
2110 FORI1=1TO4:PRINTTAB(N+1)"\XA5\XD7\XA7":NEXT
2120 PRINT"\X11\X11WELCOME TO GRUNGY TOWERS"
2130 PRINT"HIT ANY KEY TO START"
2140 GETC$:IFC$=""THEN2140
2150 RETURN
3000 M$(J)=M$(J)+CHR$(I+64):M$(I)=M$(I)+CHR$(J+64):RETURN
3010 PRINTTAB(N+2)"\XA7\XBE"
3020 PRINTTAB(N+2)"\X12\XA9\XDF"
3030 PRINTTAB(N+1)"\X12\XA9  \XDF"
3040 PRINTTAB(N)"\XBC\X12\XC0\XC0\XC0\XC0\X92\XBE"
3050 PRINTTAB(N+1)"\XBF\XBF\XBF\XBF\XA5"
3060 PRINTTAB(N+1)"\XBF\XA9\XDF\XBF\XA5"
3070 PRINTTAB(N+1)"\XBF\XA5\XA7\XBF\XA5"
3080 PRINTTAB(N+1)"\XBF\XCC\XBA\XBF\XA5"
3090 PRINTTAB(N+1)"\XBF\X12\XBB\XBB\X92\XBF\XA5"
3100 RETURN
3400 IF X=R THEN R$="HERE ":RETURN
3500 R$="IN THE "+R$(X)
3510 IF X=12 THEN R$="ON THE "+R$(X)
3520 R$=R$+" ":RETURN
3900 GOSUB 4000:?:?:RETURN
4000 Z=39-POS(0):IFLEN(Z$)<=ZTHENPRINTZ$;:RETURN
4010 IFZ<2THENPRINT:GOTO4000
4020 FORZ1=ZTO1STEP-1:IFMID$(Z$,Z1,1)<>" "THENNEXTZ1
4030 IFZ1=0THENPRINT:GOTO4000
4040 PRINTLEFT$(Z$,Z1)
4050 Z1=Z1+1:IF Z1>LEN(Z$) THEN RETURN
4060 IF MID$(Z$,Z1,1)=" " GOTO 4050
4070 Z$=MID$(Z$,Z1):GOTO 4000
